plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

group = 'io.blockmason'
version = '0.1.0'

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'Link SDK for Java'
        description = 'Blockmason Link SDK for Java/Android applications.'
        url = 'https://blockmason.link/'

        developers {
          developer {
            id = 'devin'
            name = 'Devin Canterberry'
            email = 'devin@blockmason.io'
          }
        }

        licenses {
          license {
            name = 'MIT'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/blockmason/link-sdk.java.git'
          developerConnection = 'scm:git:ssh://git@github.com/blockmason/link-sdk.java.git'
          url = 'https://github.com/blockmason/link-sdk.java'
        }
      }
    }
  }

  repositories {
    maven {
      url = "$buildDir/repos/releases"
    }
  }
}

signing {
  useInMemoryPgpKeys(findProperty('SIGNING_KEY'), '')
  sign publishing.publications.mavenJava
}

jar {
  manifest {
    attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
  }
}

repositories {
  jcenter()
}

dependencies {
  implementation 'org.json:json:20180813'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

test {
  useJUnitPlatform()
}
